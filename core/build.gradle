plugins {
    // it's a java library. What did you expect?
    id 'java-library'

    // For Sonarcloud code coverage testing, these are useful.
    id 'org.sonarqube' version '3.3'
    id 'jacoco'

    // For mutation testing, this makes our lives a lot easier.
    id 'info.solidsoft.pitest' version '1.7.4'

    // For publishing to Maven Central, we'll want these.
    id 'maven-publish'
    id 'signing'

    // To be able to use Spotless in the project, this will be needed.
    id 'com.diffplug.spotless' version '6.3.0'
}

group('io.github.lucasstarsz.fastj')
version('1.7.0-SNAPSHOT')
description('An open source, Java-based 2D game engine.')


/* ********************* *
 * General Configuration *
 * ********************* */


sourceSets {
    main.java.srcDirs = ['main/java']
    main.resources.srcDirs = ['main/resources']
    test.java.srcDirs = ['test/java']
    test.resources.srcDirs = ['test/resources']
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))
java.withSourcesJar()
java.withJavadocJar()

javadoc.source(sourceSets.main.allJava)
javadoc.failOnError(false)
javadoc.options.links = ['https://docs.oracle.com/en/java/javase/17/docs/api/', 'https://www.slf4j.org/apidocs/']

sourcesJar.from(sourceSets.main.allSource)
javadocJar.from(javadoc.destinationDir)
artifacts.archives(sourcesJar)
artifacts.archives(javadocJar)

// Java modules need this in order for the module path to be inferred based on module-info.java files.
plugins.withType(JavaPlugin).configureEach {
    java.modularity.inferModulePath = true
}

repositories.mavenCentral()
dependencies.testImplementation(dependencies.platform('org.junit:junit-bom:5.8.2'))
dependencies.testImplementation('org.junit.jupiter:junit-jupiter:5.8.2')
dependencies.testRuntimeOnly("org.junit.platform:junit-platform-launcher")
dependencies.api('org.slf4j:slf4j-api:2.0.0-alpha6')
dependencies.testImplementation ('org.slf4j:slf4j-simple:2.0.0-alpha6')


/* ********************* *
 *     Unit Testing      *
 * ********************* */


import org.gradle.api.internal.tasks.testing.results.DefaultTestResult

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {

        def totalTestTime = 0

        afterTest { desc, DefaultTestResult result ->
            totalTestTime += result.endTime - result.startTime
        }

        afterSuite { desc, DefaultTestResult result ->
            if (!desc.parent) { // will match the outermost suite
                def passFailSkip = "$result.successfulTestCount passed, $result.failedTestCount failed, $result.skippedTestCount skipped"
                def results = "Test Suite Results: $result.resultType ($result.testCount tests, $passFailSkip) in $totalTestTime ms."

                def startPipe = '|  '
                def endPipe = '  |'
                def repeatLength = startPipe.length() + results.length() + endPipe.length()
                def dashes = '-' * repeatLength

                logger.info(String.format('%n%n%s%n%s%s%s%n%s%n%n', dashes, startPipe, results, endPipe, dashes))

                if (result.resultType != TestResult.ResultType.SUCCESS) {
                    System.exit(0)
                }
            }
        }
    }
}


/* ********************* *
 *   Mutation Testing    *
 * ********************* */


pitest {
    junit5PluginVersion = '0.15'
    mainSourceSets = [sourceSets.main]
    testSourceSets = [sourceSets.test]
    targetClasses = ['tech.fastj.*']
    targetTests = ['tests.integration.*', 'tests.unit.*']
    verbose = true
}


/* ********************* *
 *     Code Coverage     *
 * ********************* */


sonarqube.properties {
    property 'sonar.projectKey', 'fastjengine_FastJ'
    property 'sonar.organization', 'fastjengine'
    property 'sonar.host.url', 'https://sonarcloud.io'
}

jacocoTestReport {
    dependsOn(test) // tests are required to run before generating the report
    reports.xml.required.set(true)
    reports.csv.required.set(false)
    reports.xml.destination(layout.buildDirectory.dir('build/reports/jacoco/test/jacocoTestReport.xml').get().asFile)
}


/* ********************* *
 *      Publishing       *
 * ********************* */


publishing.publications {
    fastjPublish(MavenPublication) {

        groupId = project.group
        version = project.version
        artifactId = 'fastj-core'

        pom {
            name = 'FastJ Game Engine'
            description = project.description
            url = 'https://github.com/fastjengine/FastJ'

            scm {
                connection = 'scm:git:https://github.com/fastjengine/FastJ.git'
                developerConnection = 'scm:git:https://github.com/fastjengine/FastJ.git'
                url = 'https://fastj.tech'
            }

            licenses {
                license {
                    name = 'MIT License'
                    url = 'https://github.com/fastjengine/FastJ/blob/main/LICENSE.txt'
                }
            }

            developers {
                developer {
                    id = 'andrewd'
                    name = 'Andrew Dey'
                    email = 'andrewrcdey@gmail.com'
                }
            }
        }

        from(components.java)
    }
}

publishing.repositories.maven {
    url = version.toString().endsWith('-SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    credentials.username = System.getenv('ossrhUsername')
    credentials.password = System.getenv('ossrhPassword')
}


/* ************************* *
 *   Spotless Integration   *
 * ************************* */


spotless {
    java {
        trimTrailingWhitespace()
        importOrder(
                'tech.fastj',
                'tech.fastj.feature',
                'java',
                '\\#'
        )
        removeUnusedImports()
    }
}

plugins {
    // it's a java library. What did you expect?
    id 'java-library'

    // To be able to use Spotless in the project, this will be needed.
    id 'com.diffplug.spotless' version '6.3.0'
}

group('io.github.lucasstarsz.fastj')
version('1.7.0-SNAPSHOT')
description('An open source, Java-based 2D game engine.')


wrapper.gradleVersion = '7.4'
wrapper.distributionType = Wrapper.DistributionType.ALL

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))
java.withSourcesJar()
java.withJavadocJar()

// Java modules need this in order for the module path to be inferred based on module-info.java files.
plugins.withType(JavaPlugin).configureEach {
    java.modularity.inferModulePath = true
}


/* ************************* *
 *   Spotless Integration   *
 * ************************* */


spotless {
    java {
        trimTrailingWhitespace()
        importOrder(
                'tech.fastj',
                'tech.fastj.feature',
                'java',
                '\\#'
        )
        removeUnusedImports()
    }
}

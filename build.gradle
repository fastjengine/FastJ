plugins {
    // it's a java library. What did you expect?
    id 'java-library'

    // For Sonarcloud code coverage testing, these are useful.
    id 'org.sonarqube' version '3.1.1'
    id 'jacoco'

    // For publishing to Maven Central, we'll want these.
    id 'maven-publish'
    id 'signing'
}

group('io.github.lucasstarsz.fastj')
version('1.5.0-SNAPSHOT')
description('An open source, Java-based 2D game engine.')

import org.gradle.api.internal.tasks.testing.results.DefaultTestResult

import java.nio.file.Files
import java.nio.file.Path


/* ********************* *
 * General Configuration *
 * ********************* */


def javaVersion = JavaVersion.VERSION_11

java.withSourcesJar()
java.withJavadocJar()
java.sourceCompatibility(javaVersion)
java.targetCompatibility(javaVersion)

javadoc.source(sourceSets.main.allJava)
javadoc.failOnError(false)
javadoc.options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api/']

// Java modules need this in order for the module path to be inferred based on module-info.java files.
plugins.withType(JavaPlugin).configureEach {
    java.modularity.inferModulePath = true
}

repositories.mavenCentral()
dependencies.testImplementation(dependencies.platform('org.junit:junit-bom:5.7.2'))
dependencies.testImplementation('org.junit.jupiter:junit-jupiter:5.7.2')


/* ********************* *
 *   Example Programs    *
 * ********************* */


sourceSets {
    example {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

        task example(type: Exec) {
            doFirst {
                if (!project.hasProperty('toRun')) {
                    throw new IllegalArgumentException(
                            'You need to specify which example using the \"toRun\" property!\nE.G. \"./gradlew example -PtoRun=hellofastj\"'
                    )
                }

                if (!Files.exists(Path.of(projectDir.toString(), 'src', 'example', 'java', 'tech', 'fastj', 'example', project.getProperty('toRun') as String))) {
                    throw new IllegalArgumentException(
                            "The example ${project.getProperty('toRun')} does not exist."
                    )
                }
            }

            dependsOn(tasks.compileExampleJava)
            description = 'Runs a FastJ example program.'
            group = 'Execution'

            commandLine(
                    'java',
                    '-classpath', sourceSets.example.runtimeClasspath.getAsPath(),
                    "tech.fastj.example.${project.hasProperty('toRun') ? project.getProperty('toRun') : ''}.Main"
            )
        }
    }
}


/* ********************* *
 *     Unit Testing      *
 * ********************* */


tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {

        def totalTestTime = 0

        afterTest { desc, DefaultTestResult result ->
            totalTestTime += result.endTime - result.startTime
        }

        afterSuite { desc, DefaultTestResult result ->
            if (!desc.parent) { // will match the outermost suite
                def passFailSkip = "$result.successfulTestCount passed, $result.failedTestCount failed, $result.skippedTestCount skipped"
                def results = "Test Suite Results: $result.resultType ($result.testCount tests, $passFailSkip) in $totalTestTime ms."

                def startItem = '|  '
                def endItem = '  |'
                def repeatLength = startItem.length() + results.length() + endItem.length()
                def dashes = '-' * repeatLength

                logger.info(String.format('%n%n%s%n%s%s%s%n%s%n%n', dashes, startItem, results, endItem, dashes))

                if (result.resultType != TestResult.ResultType.SUCCESS) {
                    System.exit(0)
                }
            }
        }
    }
}


/* ********************* *
 *     Code Coverage     *
 * ********************* */


sonarqube.properties {
    property 'sonar.projectKey', 'fastjengine_FastJ'
    property 'sonar.organization', 'fastjengine'
    property 'sonar.host.url', 'https://sonarcloud.io'
}

jacocoTestReport {
    dependsOn(test) // tests are required to run before generating the report
    reports.xml.required.set(true)
    reports.csv.required.set(false)
    reports.xml.destination(layout.buildDirectory.dir('build/reports/jacoco/test/jacocoTestReport.xml').get().asFile)
}


/* ********************* *
 *      Publishing       *
 * ********************* */


def shouldPublish = System.getenv('ossrhUsername') != null && System.getenv('ossrhPassword') != null
publish.onlyIf { shouldPublish }

if (shouldPublish) {
    publishing.publications {
        fastjPublish(MavenPublication) {

            groupId = project.group
            version = project.version

            pom {
                name = 'FastJ Game Library'
                description = project.description
                url = 'https://github.com/fastjengine/FastJ'

                scm {
                    connection = 'scm:git:https://github.com/fastjengine/FastJ.git'
                    developerConnection = 'scm:git:https://github.com/fastjengine/FastJ.git'
                    url = 'https://tech.fastj'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/fastjengine/FastJ/blob/main/LICENSE.txt'
                    }
                }

                developers {
                    developer {
                        id = 'andrewd'
                        name = 'Andrew Dey'
                        email = 'andrewrcdey@gmail.com'
                    }
                }
            }

            from(components.java)
        }
    }

    publishing.repositories.maven {
        url = version.toString().endsWith('-SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        credentials.username = System.getenv('ossrhUsername')
        credentials.password = System.getenv('ossrhPassword')
    }

    signing.sign publishing.publications.fastjPublish
}
